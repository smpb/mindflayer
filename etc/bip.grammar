#
# Regexp::Grammars definition for Bip - bip.milkypond.org - IRC logs
#

#<logfile: parser_log>
#<debug: match>

<nocontext:>

<DATE> ( <TOPIC> | <JOIN> | <KICK> | <MODE> | <QUIT> | <AKA> | <INVITE> | <EMOTE> | <DIALOG> )


<rule: DATE>          <DAY> <TIME>
<rule: DAY>           [-\d]+
<rule: TIME>          [:\d]+

<rule: TOPIC>         <.STATUS> ( ( <FROM><SOURCE> changed topic of <CHANNEL> to: <CONTENT> ) |
                                  ( Topic set by <FROM><SOURCE> \[ <CONTENT> \]) |
                                  ( Topic for <CHANNEL>: <CONTENT> ) )
<rule: JOIN>          <.STATUS> ( ( <FROM><SOURCE> has joined <CHANNEL> ) |
                                  ( <NAME>@<HOST> has joined the channel with the alias '<FROM>' ) |
                                  ( Connected to server ) )
<rule: KICK>          <.STATUS> ( ( <TO> has been kicked by <FROM><SOURCE> \[ <CONTENT> \] ) |
                                  ( \*<FROM>\* kicked <TO> ) )
<rule: MODE>          <.STATUS> mode/<CHANNEL> \[ <CONTENT> \] by <FROM>?
<rule: QUIT>          <.STATUS> ( ( <FROM><SOURCE> has (?: quit | left ) <CHANNEL>? ( \[ <CONTENT> \] )? ) |
                                  ( <FROM> has left the room \(<NAME>@<HOST>\) ) |
                                  ( Disconnected from server ) )
<rule: AKA>           <.STATUS> '?<FROM>'? is now known as '?<TO>'?
<rule: INVITE>        <.STATUS> <FROM> invited <TO>(@<HOST>)*

<rule: STATUS>        ( -!- | ( [><] subetha[`\w]*<SOURCE>: [\*\.><]* ) )

<rule: EMOTE>         [><] ( ( subetha[`\w]*<SOURCE>: \* \*<FROM>\* <CONTENT> )  | ( \* <FROM><SOURCE>  <CONTENT> ) )
<rule: DIALOG>        [><] ( ( subetha[`\w]*<SOURCE>:    \<<FROM>\> <CONTENT> )  | (    <FROM><SOURCE>: <CONTENT> ) )

<rule: CONTENT>       .+
<rule: FROM>          [-`\{\}\[\]\|\.\w]+
<rule: TO>            [-`\{\}\[\]\|\.\w]+
<rule: CHANNEL>       \#[-\w]+

<rule: SOURCE>        ( ![~]?<NAME>@<HOST> )?
<rule: NAME>          [-`\{\}\[\]\|\.\w]+
<rule: HOST>          [-:\/\.\w]+

